import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.rehanced.lunary"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 31
        versionName "1.39"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.7
        sourceCompatibility 1.7
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/rxjava.properties'
    }

    lintOptions {
        disable 'MissingTranslation'
    }
    flavorDimensions "standard"
    productFlavors {
        googleplay {
        }

        fdroid {
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.android.support:multidex:1.0.3'
    }

    task ndkBuild(type: Exec) {
        def rootDir = project.rootDir
        def localProperties = new File(rootDir, "local.properties")
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def ndkDir = properties.getProperty('ndk.dir')

        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine ndkDir + '\\ndk-build.cmd',
                    'NDK_LIBS_OUT=main/jniLibs',
                    'NDK_PROJECT_PATH=' + rootDir + '\\app\\src',
                    'APP_BUILD_SCRIPT=jni/Android.mk',
                    '-C',
                    file('src').absolutePath
        } else {
            commandLine ndkDir + '/ndk-build',
                    'NDK_LIBS_OUT=main/jniLibs',
                    'NDK_PROJECT_PATH=' + rootDir + '/app/src',
                    'APP_BUILD_SCRIPT=jni/Android.mk',
                    '-C',
                    file('src').absolutePath
        }
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }


}

dependencies {
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'me.dm7.barcodescanner:zxing:1.8.4'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'org.web3j:core-android:2.2.1'
    googleplayImplementation 'com.google.android.gms:play-services-analytics:17.0.0'
    googleplayImplementation 'com.google.android.gms:play-services-ads:18.0.0'
    implementation 'me.zhanghai.android.patternlock:library:2.1.2'
    implementation 'com.afollestad.material-dialogs:core:0.9.4.5'
    implementation 'io.github.kobakei:ratethisapp:1.2.0'
    implementation 'com.github.apl-devs:appintro:v4.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    implementation 'me.grantland:autofittextview:0.2.1'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.2.0@aar'
    implementation("com.mikepenz:materialdrawer:5.9.1@aar") {
        transitive = true
    }
}